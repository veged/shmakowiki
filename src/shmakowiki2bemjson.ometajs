ometa ShmakoWikiToBemjson {
    keyword = 'para'
            | 'header1' | 'header2' | 'header3'
            | 'header4' | 'header5' | 'header6'
            | 'olist' | 'ulist' | 'olistItem' | 'ulistItem'
            | 'bold' | 'bold_'
            | 'italic' | 'italic_'
            | 'underline' | 'underline_'
            | 'strike' | 'strike_'
            | 'monospace' | 'monospace_'
            | 'superscript' | 'superscript_'
            | 'subscript' | 'subscript_'
            | 'link' | 'link_'
            | 'lineBreak'
            | 'escaped'
            | 'extension',

    token = [keyword:t apply(t):ans] -> ans
          | :c -> utils.htmlEscape(c),
    tokens = [token*:c] -> c,


    bold tokens:c -> { tag: 'b', content: c },
    bold_ tokens:c -> { tag: 'b', content: c },

    italic tokens:c -> { tag: 'i', content: c },
    italic_ tokens:c -> { tag: 'i', content: c },

    underline tokens:c -> { tag: 'u', content: c },
    underline_ tokens:c -> { tag: 'u', content: c },

    strike tokens:c -> { tag: 's', content: c },
    strike_ tokens:c -> { tag: 's', content: c },

    monospace tokens:c -> { tag: 'tt', content: c },
    monospace_ tokens:c -> { tag: 'tt', content: c },

    superscript tokens:c -> { tag: 'sup', content: c },
    superscript_ tokens:c -> { tag: 'sup', content: c },

    subscript tokens:c -> { tag: 'sub', content: c },
    subscript_ tokens:c -> { tag: 'sub', content: c },

    link token:c [] -> { block: 'b-link', url: c, content: c },
    link token:c tokens:cc -> { block: 'b-link', url: c, content: cc },
    link_ token:c -> { block: 'b-link', url: c, content: c },

    lineBreak :c -> { tag: 'br' },

    escaped tokens:c -> { tag: 'span', content: c },

    para tokens:c -> { elem: 'p', content: c },

    header1 tokens:c :a -> { elem: 'h1', attrs: { 'id': a }, content: c },
    header2 tokens:c :a -> { elem: 'h2', attrs: { 'id': a }, content: c },
    header3 tokens:c :a -> { elem: 'h3', attrs: { 'id': a }, content: c },
    header4 tokens:c :a -> { elem: 'h4', attrs: { 'id': a }, content: c },
    header5 tokens:c :a -> { elem: 'h5', attrs: { 'id': a }, content: c },
    header6 tokens:c :a -> { elem: 'h6', attrs: { 'id': a }, content: c },

    olist tokens:c -> { elem: 'ol', content: c },
    ulist tokens:c -> { elem: 'ul', content: c },
    olistItem tokens:c -> { elem: 'li', content: c },
    ulistItem tokens:c -> { elem: 'li', content: c },

    extension :t :c :p -> utils.getExtension(t, 'astToBemjson', function(c, p) {
        return { tag: 'div', content: utils.htmlEscape(c) };
    })(c, p),

    topInline = tokens:c -> c,

    topLevel = tokens:c -> { block: 'b-text', content: c }
}
